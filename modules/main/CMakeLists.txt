#
#     .============.
#    //  M A K E  / \
#   //  C++ DEV  /   \
#  //  E A S Y  /  \/ \
# ++ ----------.  \/\  .
#  \\     \     \ /\  /
#   \\     \     \   /
#    \\     \     \ /
#     -============'
#
# Copyright (c) 2018 Hevake and contributors, all rights reserved.
#
# This file is part of cpp-tbox (https://github.com/cpp-main/cpp-tbox)
# Use of this source code is governed by MIT license that can be found
# in the LICENSE file in the root of the source tree. All contributing
# project authors may be found in the CONTRIBUTORS.md file in the root
# of the source tree.
#

cmake_minimum_required(VERSION 3.15)

set(TBOX_MAIN_VERSION_MAJOR 1)
set(TBOX_MAIN_VERSION_MINOR 1)
set(TBOX_MAIN_VERSION_PATCH 2)
set(TBOX_MAIN_VERSION ${TBOX_MAIN_VERSION_MAJOR}.${TBOX_MAIN_VERSION_MINOR}.${TBOX_MAIN_VERSION_PATCH})

add_definitions(-DMODULE_ID="tbox.main")

set(TBOX_LIBRARY_NAME tbox_main)

set(TBOX_MAIN_HEADERS
    context.h
    main.h
    module.h)

set(TBOX_MAIN_SOURCES
    context_imp.cpp
    run_in_frontend.cpp
    run_in_backend.cpp
    error_signals.cpp
    terminate.cpp
    misc.cpp
    args.cpp
    module.cpp
    log.cpp
    trace.cpp)

#set(TBOX_MAIN_TEST_SOURCES )

add_library(${TBOX_LIBRARY_NAME} ${TBOX_BUILD_LIB_TYPE} ${TBOX_MAIN_SOURCES})
add_library(tbox::${TBOX_LIBRARY_NAME} ALIAS ${TBOX_LIBRARY_NAME})

set_target_properties(
    ${TBOX_LIBRARY_NAME} PROPERTIES
    VERSION ${TBOX_MAIN_VERSION}
    SOVERSION ${TBOX_MAIN_VERSION_MAJOR}
)

# install the target and create export-set
install(
    TARGETS ${TBOX_LIBRARY_NAME}
    EXPORT ${TBOX_LIBRARY_NAME}_targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

# get the current directory name
get_filename_component(CURRENT_DIR_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)

# install header files with directory structure
foreach(header IN LISTS TBOX_MAIN_HEADERS)
    get_filename_component(dir ${header} DIRECTORY)
    install(FILES ${header} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tbox/${CURRENT_DIR_NAME}/${dir})
endforeach()

# generate and install export file
install(
    EXPORT ${TBOX_LIBRARY_NAME}_targets
    FILE ${TBOX_LIBRARY_NAME}_targets.cmake
    NAMESPACE tbox::
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/tbox
)
